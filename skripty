pf maximalni mesic = 
IF(MAX(data[mesic]) = 
MAXX(ALL(data),data[mesic]),"#ff6347","	#00bfff")

________________________________

01 suma srazek = 

-- Scénář 1: Respektuji všechny filtry vizuálu, žádný filtr neruším, žádný nestavuji
-- tj. používám základní agregační funkce typu: SUM(),MIN(),MAX()

SUM(data[srazky])
_______________________________

02 suma srazek ruzyne = 
-- Scénář 2: Respektuji filtry vizuálu a nastavuji další,
-- tj. používám podmíněnou agregaci, tj. SUMX(FILTER()),MAXX(FILTER()),MINX(FILTER())
SUMX(
    --krok3: Funkce FILTER() vrací tabulku
    FILTER(
    --krok1: Funkce FILTER() prebira tabulku data filtrovanou vizualem    
    data
    --krok2: Funkce FILTER() nastavuje filtr na sloupecek [lokalita]
    ,data[lokalita] = "RUZYNE"),
    --krok4: Funkce SUMX() scita sloupec [srazky] 
    -- tabulky vracene funkci FILTER()
    data[srazky])
    
    
_______________________________________________

03 suma srazek vsechny roky = 
-- Scénář 3: Ruším filtry vizuálu a nenastavuji další
-- tj. použiji SUMX(ALL()),MINX(ALL())
SUMX(
    --krok1: Funkce ALL() vraci kopii tabulky data
    -- se vsemi jejimi hodnotami
    ALL(data),
    --krok2: Funkce SUMX() scita sloupec [srazky]
    -- tabulky vracene funkci ALL()
    data[srazky])


____________________________________________________

04 suma srazek rok 2000 = 
-- Scénář 4: Ruším filtry vizuálu a nastavuji si svoje
-- tj. použiju kombinaci funkcí, SUMX(FILTER(ALL())), MAXX(FILTER(ALL()))
--krok3: Funkce SUMX() scita sloupec [srazky] tabulky vracene funkci FILTER()
SUMX(
    --krok2: Funkce FILTER() nastavuje filtr na sloupec [rok]
    -- tabulky vracene funkci ALl() a pote funkce FILTER() vraci tabulku
    FILTER(
        --krok1: Funkce ALL() vraci kopii tabulky data zbavenou vsech filtru
    ALL(data),data[rok] = 2000),data[srazky])


________________________________________________________

08 suma srazek v danem roce ve dnech teplota nad 20 = 
-- suma srážek v datumech, kdy maximalni_teplota > 20
SUMX(
    FILTER(data,
    data[maximalni_teplota] > 20),
data[srazky])



_______________________________________________________

09 pocet datumu v danem roce v ruzyni kdy neprselo = 
 -- neprselo znamena, ze srazky=0

 COUNTX(
     FILTER(data,data[srazky] = 0
     -- && je "a zároveň"
     -- || je "nebo"
     && data[lokalita] = "RUZYNE"),
     data[datum])


_______________________________________________________________

10 maximalni teplota v danem roce v mesicich 7 nebo 8 = 
-- maximum ze sloupce maximalni_teplota
MAXX(
    FILTER(data,data[mesic] = 7 || data[mesic] = 8),
data[maximalni_teplota])


_______________________________________________________

11 maximum z maximalni teploty = 
-- pokud je maximalni teplota v mesicich 7 nebo 8 v danem roce
-- nejvyssi maximalni teplotou v mesicich 7 nebo 8 ve vsech letech,
-- tak vracime "gold"

IF(
-- maximum z maximalni_teplota v danem roce v mesicich 7 nebo 8
MAXX(
    FILTER(data,
    data[mesic] = 7 || data[mesic] = 8),
data[maximalni_teplota])
=
-- maximum z maximalni_teplota ve vsech letech v mesicich 7 nebo 8

MAXX(
    FILTER(
        ALL(data),
        data[mesic] = 7 || data[mesic] = 8),
data[maximalni_teplota]),"GOLD")

_____________________________________________

12 maximalni datum maximalni teploty v danem roce = 
MAXX(
    FILTER(data,
    data[maximalni_teplota] = MAX(data[maximalni_teplota])),
data[datum])

________________________________________________

13 maximalni teplota v mesici 7 v lokalite ruzyne = 
-- v danem roce, maximum z maximalni teplota

MAXX(
    FILTER(data,
    data[mesic] = 7
    && data[lokalita] = "RUZYNE"),
data[maximalni_teplota])

___________________________________________________

14 rozdil mezi maximalni a minimalni teplotou v danem roce = 
-- rozdil mezi maximem z maximalni teploty a minimem minimalni teploty
-- v danem roce

-- maximum z maximalni teploty v danem
MAX(data[maximalni_teplota])
-
MIN(data[minimalni_teplota])
